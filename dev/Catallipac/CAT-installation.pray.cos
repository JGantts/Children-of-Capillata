**CAOS2PRAY
*# Pray-File "CoC_CAT.agents"
*# DS-Name "CoC - The Catallipac"
*# Agent Animation File = "fav_place_coc_cat.c16"
*# Agent Sprite First Image = 1
*# desc = "This is the Catallipac. It is a whole new room to explore!\n\nRequires the Universal Hub!\n\nPart of Children of Capillata.\nDesigned by and images by Silvak.\nMaintained by and code by Zzzzoot."

*# attach coc_catallipac.blk coc_cat_uhcat_door.c16 coc_cat_vendor.c16 coc_cat_vendor_mouth.c16 fav_place_coc_cat.c16 coc_lib_sealift.c16
*# Link CAT-door-events.cos CAT-empathic-vendor-events.cos LIB-sealift-events.cos

* Script 1 extracted from CoC_UH.agents by Mirality REVELATION
* Thank you Mirality! -Zzzzoot

inst

enum 1 2 5
	kill targ
next
enum 1 2 24403
	kill targ
next


**Copied from DS agent help
new: comp 1 2 24403 "useful_screen" 1 0 9040
attr 304
pat: fixd 1 "useful_screen" 1 25 59 1 "WhiteOnTransparentChars"
part 1
frmt 8 8 8 18 3 0 0
pat: butt 2 "useful_screen" 13 2 122 290 0 [0 1 255] 1000 0
pat: fixd 6 "useful_screen" 15 119 306 1 "WhiteOnTransparentChars"
part 6
frmt 0 0 0 0 3 0 2
pat: fixd 3 "useful_screen" 12 22 22 1 "WhiteOnTransparentChars"
part 3
frmt 8 0 37 0 2 0 8
setv va90 wndw
divv va90 2
setv va91 wndh
divv va91 2
setv va92 wdth
divv va92 2
setv va93 hght
divv va93 2
subv va90 va92
subv va91 va93
flto va90 va91
**/
part 3
ptxt "Catilipac Version v1.2.0.4-alpha"
part 1
ptxt read "zot_CAT" 0

*kill dialog
scrp 1 2 24403 1001
	kill ownr
endm

***close text box and install the agent
scrp 1 2 24403 1000
	inst
	part 2
	anim [0 1 0]
	over
	inst

*	inject
	doif game "CoC_CAT" = 1
		targ ownr
		pat: kill 2
		pat: butt 2 "useful_screen" 13 2 122 290 0 [0 1 255] 1001 0
		part 1
		ptxt "The Catilipac is already injected."
	elif game "CoC_UH" = 0
*		oops, UH isn't installed
		targ ownr
		pat: kill 2
		pat: butt 2 "useful_screen" 13 2 122 290 0 [0 1 255] 1001 0
		part 1
		ptxt "You need to inject the Universal Hub first. Go to this webpage to get it:\n\nhttp://creatures.wikia.com/wiki/Universal_Hub"
	else
*			*********
*			***MAP***
*			*********
*			* Autogenerated by the Creatures Map Editor
*			* For more info see http://cdn.creatures.net
*
*			* Addon file: deletes existing metarooms at the same place, same background name
*			* 1 metarooms, 19 rooms, 21 doors

		mapd 100000 100000

		setv game "mapeditortmp" addm 39802 29359 1292 792 "coc_catallipac"
		mmsc 40448 29755 ""
		setv va00 addr game "mapeditortmp" 40104 40286 29585 29585 29798 29798
		rtyp va00 5
		rmsc 40195 29691 ""
		setv game "mapeditortmp_600" va00
		setv va00 addr game "mapeditortmp" 40286 40465 29585 29585 29798 29798
		rtyp va00 5
		rmsc 40375 29691 ""
		setv game "mapeditortmp_601" va00
		setv va00 addr game "mapeditortmp" 40465 40631 29585 29585 29798 29798
		rtyp va00 5
		rmsc 40548 29691 ""
		setv game "mapeditortmp_602" va00
		setv va00 addr game "mapeditortmp" 40631 40797 29585 29585 29798 29798
		rtyp va00 5
		rmsc 40714 29691 ""
		setv game "mapeditortmp_603" va00
		setv va00 addr game "mapeditortmp" 40797 40884 29585 29585 29798 29791
		rtyp va00 3
		rmsc 40840 29688 ""
		setv game "mapeditortmp_604" va00
		setv va00 addr game "mapeditortmp" 40884 40952 29585 29585 29791 29775
		rtyp va00 3
		rmsc 40918 29680 ""
		setv game "mapeditortmp_605" va00
		setv va00 addr game "mapeditortmp" 40702 40758 29501 29528 29558 29558
		rtyp va00 3
		rmsc 40730 29529 ""
		setv game "mapeditortmp_606" va00
		setv va00 addr game "mapeditortmp" 40523 40645 29393 29426 29553 29558
		rtyp va00 3
		rmsc 40584 29475 ""
		setv game "mapeditortmp_607" va00
		setv va00 addr game "mapeditortmp" 40381 40523 29393 29393 29553 29553
		rtyp va00 3
		rmsc 40452 29473 ""
		setv game "mapeditortmp_608" va00
		setv va00 addr game "mapeditortmp" 40257 40381 29417 29393 29553 29553
		rtyp va00 3
		rmsc 40319 29485 ""
		setv game "mapeditortmp_609" va00
		setv va00 addr game "mapeditortmp" 40175 40257 29450 29417 29553 29553
		rtyp va00 3
		rmsc 40216 29501 ""
		setv game "mapeditortmp_610" va00
		setv va00 addr game "mapeditortmp" 40645 40702 29426 29442 29558 29558
		rtyp va00 3
		rmsc 40673 29492 ""
		setv game "mapeditortmp_611" va00
		setv va00 addr game "mapeditortmp" 40139 40175 29464 29450 29553 29553
		rtyp va00 3
		rmsc 40157 29508 ""
		setv game "mapeditortmp_612" va00
		setv va00 addr game "mapeditortmp" 39949 40104 29585 29585 29780 29798
		rtyp va00 5
		rmsc 40026 29691 ""
		setv game "mapeditortmp_613" va00
		setv va00 addr game "mapeditortmp" 39878 39949 29632 29533 29760 29780
		rtyp va00 3
		rmsc 39913 29706 ""
		setv game "mapeditortmp_614" va00
		setv va00 addr game "mapeditortmp" 39949 40083 29533 29477 29585 29585
		rtyp va00 0
		rmsc 40016 29559 ""
		setv game "mapeditortmp_615" va00
		setv va00 addr game "mapeditortmp" 40858 40952 29488 29540 29585 29585
		rtyp va00 0
		rmsc 40905 29536 ""
		setv game "mapeditortmp_616" va00
		setv va00 addr game "mapeditortmp" 40805 40858 29476 29488 29585 29585
		rtyp va00 0
		rmsc 40831 29530 ""
		setv game "mapeditortmp_617" va00
		setv va00 addr game "mapeditortmp" 40702 40758 29442 29498 29501 29528
		rtyp va00 0
		rmsc 40730 29485 ""
		setv game "mapeditortmp_618" va00

		door game "mapeditortmp_600" game "mapeditortmp_601" 100
		door game "mapeditortmp_600" game "mapeditortmp_613" 100
		door game "mapeditortmp_601" game "mapeditortmp_602" 100
		door game "mapeditortmp_602" game "mapeditortmp_603" 100
		door game "mapeditortmp_603" game "mapeditortmp_604" 100
		door game "mapeditortmp_604" game "mapeditortmp_605" 100
		door game "mapeditortmp_604" game "mapeditortmp_616" 100
		door game "mapeditortmp_604" game "mapeditortmp_617" 100
		door game "mapeditortmp_605" game "mapeditortmp_616" 100
		door game "mapeditortmp_606" game "mapeditortmp_611" 100

		door game "mapeditortmp_606" game "mapeditortmp_618" 100
		door game "mapeditortmp_607" game "mapeditortmp_608" 100
		door game "mapeditortmp_607" game "mapeditortmp_611" 100
		door game "mapeditortmp_608" game "mapeditortmp_609" 100
		door game "mapeditortmp_609" game "mapeditortmp_610" 100
		door game "mapeditortmp_610" game "mapeditortmp_612" 100
		door game "mapeditortmp_611" game "mapeditortmp_618" 100
		door game "mapeditortmp_613" game "mapeditortmp_614" 100
		door game "mapeditortmp_613" game "mapeditortmp_615" 100
		door game "mapeditortmp_614" game "mapeditortmp_615" 100

		door game "mapeditortmp_616" game "mapeditortmp_617" 100

		delg "mapeditortmp_600"
		delg "mapeditortmp_601"
		delg "mapeditortmp_602"
		delg "mapeditortmp_603"
		delg "mapeditortmp_604"
		delg "mapeditortmp_605"
		delg "mapeditortmp_606"
		delg "mapeditortmp_607"
		delg "mapeditortmp_608"
		delg "mapeditortmp_609"
		delg "mapeditortmp_610"
		delg "mapeditortmp_611"
		delg "mapeditortmp_612"
		delg "mapeditortmp_613"
		delg "mapeditortmp_614"
		delg "mapeditortmp_615"
		delg "mapeditortmp_616"
		delg "mapeditortmp_617"
		delg "mapeditortmp_618"
*			***********
*			***//MAP***
*			***********

*			***************
*			***FAV place***
*			***************
		new: simp 1 3 24401 "fav_place_coc_cat" 1 0 1
		attr 272
		mvto 40386 29650
		cmrt 0
		tick 10
*			*****************
*			***//FAV place***
*			*****************

*			****************
*			***Enviroment***
*			****************
		setv va00 0

** 			Water
		new: simp 1 1 167 "blnk" 1 va00 0
		attr 16
		mvto 40191 29629
		tick 150

**			 Light
		new: simp 1 1 24401 "blnk" 1 va00 0
		attr 16
		mvto 40343 29667
		emit 01 1

** 			Heat
		new: simp 1 1 24401 "blnk" 1 va00 0
		attr 16
		mvto 40542 29631
		emit 02 1

** 			Nutrients

		reps 40
			inst
			new: simp 1 1 171 "blnk" 1 va00 3000
			attr 192
			elas 0
			accg 0.4
			perm 40
			mvto rand 40200 40700 29652
		repe
*			******************
*			***//Enviroment***
*			******************

*			**********
*			***Door***
*			**********
		enum 5 5 24401
			doif ov00 = 2
				setv va00 posl
				setv va01 post
				kill targ
			endi
		next

**			ov00 - norn entry
**			ov01 - grendel entry
**			ov02 - ettin entry
* 			UH
		new: comp 2 2 24403 "coc_cat_uhcat_door" 25 0 0
* 			mvto the location of the late dummy door
		mvto va00 va01
* 			user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		setv va00 room targ
**			set the entry
		setv ov00 1
		setv ov01 1
		setv ov02 1
		pat: butt 1 "coc_cat_uhcat_door" 14 4 -33 22 0 [] 1000 0
		pat: butt 2 "coc_cat_uhcat_door" 15 3 -33 45 0 [] 1001 0
		pat: butt 3 "coc_cat_uhcat_door" 16 2 -33 70 0 [] 1002 0

*			Catilapac
		new: comp 2 2 24404 "DS door" 13 43 0
		mvto 40829 29582
* 			user can click on door and when s/he does run script 1
		attr 4
		clac 0
		bhvr 1
		tran 0 0
* 			link the rooms, spread the CA around
		link va00 room targ 5
** 			set the entry
		setv ov00 1
		setv ov01 1
		setv ov02 1
*			grendel
		pat: butt 1 "ds door" 78 3 37 71 0 [] 1001 0
		part 1
		pose 1
*			Ettin
		pat: butt 2 "ds door" 81 3 57 97 0 [] 1002 0
		part 2
		pose 1
* 			Norn
		pat: butt 3 "ds door" 84 3 30 123 0 [] 1000 0
		part 3
		pose 1
*			************
*			***//Door***
*			************

*			**************
*			***Heat Pan***
*			**************
**			CAT Heat Pan Incubator
		new: simp 2 22 4 "blnk" 1 0 0
		attr 16

		mvto 40602 29529
		tick 50
		emit 15 .25

		new: simp 2 22 4 "blnk" 1 0 0
		attr 16

		mvto 40666 29531
		tick 60
		emit 15 .25
*			****************
*			***//Heat Pan***
*			****************

***************
******Lift*****
***************

*new ZootLift
		new: vhcl 3 1 24401 "coc_lib_sealift" 2 0 310
*agnt lift = targ
		seta va02 targ
*enum LiftState = Stationary
		setv ov00 0
*
		cabn -11 -10 110 134
*Can't be picked up, Visible to creatures, Doesn't suffer physics
		attr 0
*Creatures can push, Creatures can pull
		bhvr 3
*
		cabw 12
*agnt ButtonCurrent = null
		seta ov01 null
*agnt ButtonDestination = null
		seta ov02 null
*numb StartY = 0
		setv ov10 0
*numb StopY = 1
		setv ov11 1
*agnt From = null
		seta ov04 null
*enum PreviousDirection = GoingUp
		setv ov20 -1

*agnt previousButton = null
		seta va00 null
*numb? previousButtonsRoom = null
		setv va01 -1
****************
***Top Button***
****************
*new ZootLiftButton
		new: simp 2 12 24401 "coc_lib_sealift" 2 2 310
*agnt Lift = lift
		seta ov03 va02
*agnt ButtonAbove = previousButton
		seta ov01 va00
*agnt ButtonBelow = null
		seta ov02 null
*agnt previousButton = targ
		seta va00 targ
*if(ButtonAbove != null)
		doif ov01 <> null
*	targ ButtonAbove
			targ ov01
*	ButtonAbove.ButtonBellow = previousButton
			seta ov02 va00
*	targ previousButton
			targ va00
		endi
*
		mvto 40399 29478
*agnt From = null
		seta ov04 null
*enum ButtonState = NoLift
		setv ov00 0
*numb TargetX = posb + 10
		setv ov10 posb
		addv ov10 10
*if(previousButtonsRoom != null)
		doif va01 <> -1
*link previousButoo
			link va01 room targ 100
		endi
*numb previousButtonsRoom = room targ
		setv va01 room targ
*Mouse can activate
		attr 4
*Creatures can push
		bhvr 1

************
***Bottom***
************
*new ZootLiftButton
		new: simp 2 12 24401 "coc_lib_sealift" 2 2 310
*agnt Lift = lift
		seta ov03 va02
*agnt ButtonAbove = previousButton
		seta ov01 va00
*agnt ButtonBelow = null
		seta ov02 null
*agnt previousButton = targ
		seta va00 targ
*if(ButtonAbove != null)
		doif ov01 <> null
*	targ ButtonAbove
			targ ov01
*	ButtonAbove.ButtonBellow = previousButton
			seta ov02 va00
*	targ PreviousButton
			targ va00
		endi
*
		mvto 40550 29725
*agnt From = null
		seta ov04 null
*enum ButtonState = NoLift
		setv ov00 0
*numb TargetX = posb + 10
		setv ov10 posb
		addv ov10 10
*if(previousButtonsRoom != null)
		doif va01 <> -1
*link previousButoo
			link va01 room targ 100
		endi
*numb previousButtonsRoom = room targ
		setv va01 room targ
*Mouse can activate
		attr 4
*Creatures can push
		bhvr 1

*targ previousButton
		targ va00
*enum previousButton.ButtonState = HasLift
		setv ov00 1
*numb moveLiftTo = previousbutton.TargetX
		setv va03 ov10
*targ lift
		targ va02
*lift.ButtonCurrent = previousButton
		seta ov01 va00
*numb moveLiftTo = moveLiftTo - lift.hght
		subv va03 hght
*mvto **** moveLiftTo
		mvto 40432 va03

************
***//Lift***
************

*			********************
*			***EMPHATIC VENDOR**
*			********************
		new: comp 2 23 24401 "coc_cat_vendor" 5 0 0
		attr 0
		bhvr 3
		pat: butt 1 "coc_cat_vendor" 17 7 -108 -45 0 [] 1000 0
		pat: dull 2 "coc_cat_vendor_mouth" 0 -111 -12 1
		pat: butt 3 "coc_cat_vendor" 5 2 -60 5 0 [] 1001 1
		pat: butt 4 "coc_cat_vendor" 7 2 -60 31 0 [] 1002 1
		pat: butt 5 "coc_cat_vendor" 9 2 -60 56 0 [] 1003 1

		pat: butt 6 "coc_cat_vendor" 11 2 -193 7 0 [] 1004 1
		pat: butt 7 "coc_cat_vendor" 13 2 -193 33 0 [] 1005 1
		pat: butt 8 "coc_cat_vendor" 15 2 -193 58 0 [] 1006 1

		setv ov00 1
*			** Save Meta Room for Checking for Hungry Creatures
		setv ov70 meta
		part 1
		frat 5
		anim [1 2 3 2 255]
		mvto 40081 29671
*			**********************
*			***//EMPATHIC VENDOR**
*			**********************

*			**********
*			***Robot**
*			**********
		new: simp 2 21 20 "robot_toy" 23 0 2000
		attr 199
*			act1, act2, hit, pickup
		bhvr 43
		elas 20
		accg 3
		aero 10
		fric 70
		perm 60

		puhl -1 25 50

		setv ov10 rand 0 1
		doif ov10 eq 0
			setv ov10 -1
		endi
		doif ov10 lt 0
			pose 0
		else
			pose 12
		endi


		mvto 40400 29700
*			************
*			***//Robot**
*			************
		setv game "CoC_CAT" 1
		kill ownr
	endi
endm

*rscr
*enum 1 2 5
*	kill targ
*next
*enum 1 2 24403
*	kill targ
*next
