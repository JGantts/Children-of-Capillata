* Script 3 extracted from CoC_DAE.agents by Mirality REVELATION

***They all use one script, I'm a space saver***
***(unlike Creatures labs, the DS doors each***
***had their own set of scripts, talk about wasting space**
***but my comments could be shorter, I'll make an attempt at that in the future***
***but for now...
scrp 1 1 24402 9
* get the setting for the metaroom and store in in va01
	sets va00 name "type"
	rtar 3 3 24401
	doif va00 = "light"
		setv va01 ov01
	elif va00 = "heat"
		setv va01 ov02
	elif va00 = "water"
		setv va01 ov03
	endi
* don't want to run around trying to find inexistant variables for the wrong agent ;)
	targ ownr
	addv va01 1
*	actualy emit it
	doif name "type" = "light"
		emit 1 va01
	elif name "type" = "heat"
		emit 2 va01
	elif name "type" = "water"
		emit 3 va01
	endi
endm

*********************
***Control Scripts***
*********************

scrp 3 3 24401 1
	lock

	clac -1
	doif ov00 eq 0
		sndc "env1"
		part 0
		anim [0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15]
		over
* light button
		pat: butt 1 "env_con" 16 5 25 25 0 [] 1000 0
		part 1
*		go to correct pose, either default or last set
		pose ov01

		pat: butt 2 "env_con" 21 5 84 26 0 [] 1001 0
		part 2
		pose ov02

		pat: butt 3 "env_con" 28 2 71 38 0 [] 1000 0

		pat: butt 4 "env_con" 26 2 53 63 0 [] 1001 0
		setv ov00 1

		pat: butt 5 "env_con" 30 2 50 37 0 [] 1002 0

		prt: inew 0 "input" "" 60 88 1010
		prt: onew 0 "output" "" 74 88
	else
		sndc "env2"
		prt: bang 100
		prt: izap 0
		prt: ozap 0
		pat: kill 1
		pat: kill 2
		pat: kill 3
		pat: kill 4
		pat: kill 5
		part 0
		anim [15 14 13 12 11 10 9 8 7 6 5 4 3 2 1 0]
		over
		setv ov00 0
	endi
	clac 0
endm

scrp 3 3 24401 1000
	sndc "beep"
	part 3
	anim [0 1 0 1 0]

	doif ov99 eq 0


		doif ov01 lt 4
			addv ov01 1
		else
			setv ov01 0
		endi
		part 1
		pose ov01
	else

		doif ov03 lt 4
			addv ov03 1
		else
			setv ov03 0
		endi
		part 1
		pose ov03
	endi
endm

scrp 3 3 24401 1001
	sndc "beep"
	part 4
	anim [0 1 0 1 0]


	doif ov02 lt 4
		addv ov02 1
	else
		setv ov02 0
	endi
	part 2
	pose ov02
endm

scrp 3 3 24401 1002
	sndc "beep"
	inst

	doif ov99 eq 0
		part 5
		pose 1
		part 1
		pose ov03
		setv ov99 1

	else
		part 5
		pose 0
		part 1
		pose ov01
		setv ov99 0
	endi
endm

scrp 3 3 24401 1010
	doif _p1_ lt 0
		mesg writ ownr 1000
	elif _p1_ gt 0
		mesg writ ownr 1001
	endi
	prt: send 0 _p1_
endm

*rscr
*enum 3 3 24401
*	kill targ
*next
*enum 1 1 24402
*	kill targ
*next
