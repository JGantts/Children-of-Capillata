* Script 4 extracted from CoC_DAE.agents by Mirality REVELATION

*Lift
*		new ZootC3Lift()
new: vhcl 3 1 24402 "lift" 23 0 50
*		lift = targ
seta va00 targ
bhvr 3
attr 12
clac -1
elas 0
*map position lift
mvto 43888 29630
*		From? = null		
seta ov99 null
*		IsActive = false
setv ov00 0
*		IsMoving = false
setv ov80 0
*		FromButton? = null
seta ov16 null
*		ButtonCurrent = promiseNotNullAfterInstall
seta ov30 null
*		ButtonDestination? = null
seta ov31 null
*		Door = newPart()
pat: dull 1 "lift" 4 0 0 200
cabn 0 0 121 144
cabw 5
*		UpButton = newPart()
pat: butt 2 "lift" 16 7 52 68 0 [] 2000 1
*		DownButton = newPart()
pat: butt 3 "lift" 16 7 52 82 0 [] 2001 1
*		UpButton.Pose.4
part 2
anim [4]
*		UpButton.Pose.5
part 3
anim [5]
*		Door.Pose.Open
part 1
anim [7]

*Top Button
*		new ZootC3LiftButton
new: simp 2 12 24402 "lift" 2 14 5000
*		currentButton = targ
seta va01 targ
bhvr 1
attr 4
perm 10
clac 0
elas 0
*map position top
mvto 43843 29532
*		IsActive = false
setv ov00 0
*		Lift = lift
seta ov16 va00
*		ButtonAbove? = null
seta ov20 null
*		ButtonBelow? = null
seta ov21 null
*		Pose.Off
pose 0
*		roomId1 = Room()
setv va66 room targ

*		previousButton = currentButton
seta va02 va01

*Middle Button
*		new ZootC3LiftButton
new: simp 2 12 24402 "lift" 2 14 5000
*		currentButton = targ
seta va01 targ
bhvr 1
attr 4
perm 10
clac 0
elas 0
*map position middle
mvto 43843 29708
*		IsActive = false
setv ov00 0
*		Lift = lift
seta ov16 va00
*		ButtonAbove? = previousButton 
seta ov20 va01
*		ButtonBelow? = null
seta ov21 null
*		previousbutton.ButtonBelow? = currentButton
targ va02
seta ov21 va01
*		lift.ButtonCurrent = currentButton
targ va00
seta ov30 va01
*		targ currentButton
targ va01
*		Pose.Off
pose 0
*		roomId2 = Room()
setv va67 room targ
*		linkSmells(roomId1, roomId2)
link va66 va67 100

*		previousButton = currentButton
seta va02 va01

*BottomButton
*		new ZootC3LiftButton
new: simp 2 12 24402 "lift" 2 14 5000
*		currentButton = targ
seta va01 targ
bhvr 1
attr 4
perm 10
clac 0
elas 0
*map position bottom
mvto 43843 29955
*		IsActive = false
setv ov00 0
*		Lift = lift
seta ov16 va00
*		ButtonAbove? = previousButton 
seta ov20 null
*		ButtonBelow? = null
seta ov21 va01
*		previousbutton.ButtonBelow? = currentButton
targ va02
seta ov21 va01
*		targ currentButton
targ va01
*		Pose.Off
pose 0
*		roomId1 = Room()
setv va66 room targ
*		linkSmells(roomId2, roomId1)
link va66 va67 100

*ZootC3LiftButton.CallLift:Push
scrp 2 12 24402 1
	inst
	lock
*	lift = Lift
	seta va88 ov16
*	foreach(ZootLift)
	enum 2 12 24402
*		if(targ.Lift = lift)
		doif ov16 = va88
*			if(someoneIsActive || targ.IsActive)
*				someoneIsActive = true
			addv va66 ov00
		endi
	next
	targ mv16
*	if(Lift.IsActive)
*		someoneIsActive = true
	addv va66 ov00
*	liftPlus20 = Lift.posy + 20
*	liftMinus20 = Lift.posy - 20
	setv va51 posy
	setv va52 posy
	targ ownr
	addv va51 20
	subv va52 20
*	if(!someoneIsActive)
	doif va66 = 0
*		if(self.posy > liftPlus20 || self.posy < liftMinus20)
		doif posy > va51 or posy < va52
*
*			if(from != null && from = pntr)
			doif pntr = from
*				From? = pntr
				seta va99 pntr
*			else
			else
*				From? = null
				seta va99 null
			endi
*			foreach(ZootC3LiftButton)
			enum 2 12 24402
*				if(targ.Lift = lift)
				doif ov16 = va88
*					targ.pose.0
					pose 0
*					targ.IsActive = false
					setv ov00 0
				endi
			next
			snde "but1"
*			Pose.1
			pose 1
*			IsActive = true
			setv ov00 1
			inst
*			Lift.ActivateSelf(fromButton: unid, fromPntr?: fromPntr?)
			mesg wrt+ ov16 1500 unid va99 0
			slow
		else
			anim [1]
			over
			anim [0]
			over
		endi
	endi
endm

*ZootC3LiftButton.DeactivateSelf
scrp 2 12 24402 1501
	inst
	lock
*	pose.0
	anim [0]
*	IsActive = false
	setv ov00 0
endm

*ZootC3Lift.GoUp:Push
scrp 3 1 24402 1
	inst
*	fromCreature? = null
	seta va99 null
	targ from
*	if(from.family == Creature)
	doif fmly eq 4
*		from.Stim(Wait, 0)
		stim writ targ 75 0
*		fromCreature? = from
		seta va99 targ
	endi
	targ ownr
*	self.From? = fromCreature?
	seta ov99 va99
*	self.FindButtonUpAndGo()
	mesg writ ownr 2000
endm

*ZootC3Lift.GoDown:Pull
scrp 3 1 24402 2
	inst
*	fromCreature? = null
	seta va99 null
	targ from
*	if(from.family == Creature)
	doif fmly eq 4
*		from.Stim(Wait, 0)
		stim writ targ 75 0
*		fromCreature? = from
		seta va99 targ
	endi
	targ ownr
*	self.From? = fromCreature?
	seta ov99 va99
*	self.FindButtonDownAndGo()
	mesg writ ownr 2001
endm


*ZootC3Lift.ActivateSelf(fromButtonId, fromPntr?)
scrp 3 1 24402 1500
	inst
	lock
*	if(Active == true)
	doif ov00 eq 0
*		Active = true
		setv ov00 1
		inst
*		FromButtonId = fromButtonId
		setv ov70 _p1_

*		if(fromPntr? != null)
		doif _p2_ <> null
*			From? = fromPntr?
			seta ov99 pntr
		endi

		snde "clik"
		tick 1
		slow
	endi

endm

*ZootC3Lift.Timer:Timer
scrp 3 1 24402 9
	lock
*	fromButtonId = FromButtonId
	setv va17 ov70
	inst
*	if(IsMoving = false)
	doif ov80 = 0
		inst
*		if(From? = pntr)
		doif ov99 = pntr
*			foreachTouching(Grendel)
			etch 4 2 0
*Zombify it and face it forward so it'll fit
				inst
				doif targ <> null
*
					nohh
*					targ.Zombify()
					zomb 1
					pose 80
					dirn 1
					wait 4
					inst
					doif targ <> null
*						ownr.PickUp(targ)
						spas ownr targ
					endi
					wait 2
					inst
*					if(targ != null && targ.Carrier == null)
					doif targ <> null
						doif carr = null
*							targ.Unzombify()
							zomb 0
						endi
					endi

				endi
			next
			targ ownr
*			ownr.PickUp(TouchingCabin(Creatures All All))
			gpas 4 0 0 1
*		else if(From? != pntr && From? != null)
		elif ov99 <> pntr and ov99 <> null
			targ ov99
*			if(From.Genus = Grendel)
			doif gnus = 2
*Zombify it and face it forward so it'll fit
				inst
*				From.Zombify()
				zomb 1
				pose 80
				dirn 1
				wait 4
				inst
*				ownr.PickUp(From)				
				spas ownr targ
				wait 2
				inst
*				From.Unzombify()
				zomb 0
			else
*				ownr.PickUp(From)	
				spas ownr targ
			endi
			targ ownr
		endi

		inst
*		self.Door.Animate.Close
		part 1
		anim [7 6 5 4 3 2 1 0]
		over
*		IsMoving = true
		setv ov80 1
	endi
*	foreach(ZootC3Lift)
	enum 2 12 24402
*		if(targ.Lift = self)
		doif ov16 = ownr
*			if(targ.unid = fromButtonID)
			doif unid = va17
*				fromButton = targ
				seta va16 targ
				targ ownr
*				FromButton = FromButton
				seta ov16 va16
*				distance = RelativeY(self, FromButton) - 20
				setv va02 rely ownr ov16
				subv va02 20
*				absDistance = AbsoluteValue(distance)
				setv va03 va02
				absv va03
*				if(absDistance > 200)
				doif va03 > 200
*					velocity = 10
					setv va04 10
					tick 6
*				else if(absDistance > 100 && absDistance <= 200)
				elif va03 > 100 and va03 <= 200
*					velocity = 8
					setv va04 8
					tick 4
*				else if(absDistance > 50 && absDistance <= 100)
				elif va03 > 50 and va03 <= 100
*					velocity = 5
					setv va04 5
					tick 2
*				else if(absDistance > 10 && absDistance <= 50)
				elif va03 > 10 and va03 <= 50
*					velocity = 3
					setv va04 3
					tick 1
*				else if(absDistance <=10)
				elif va03 <= 10
*					velocity = 1
					setv va04 1
					tick 1
				endi
			endi
		endi
	next
*Direction is up
*	if(distance < 0)
	doif va02 < 0
*		velocity = -AbsoluteValue(velocity)
		absv va04
		negv va04
		part 0
		anim [3]
		bhvr 0
		part 2
		anim [6]
		part 3
		anim [6]
		velo 0 va04
*	else if(distance < 0)
	elif va02 > 0
*		velocity = AbsoluteValue(velocity)
		absv va04
		part 0
		anim [2]
		bhvr 0
		part 2
		anim [6]
		part 3
		anim [6]
		velo 0 va04
*	else if(distance == 0)
	elif va02 = 0
*		Velocity = (x: 0,y: 0)
		velo 0 0
		tick 0
*		IsMoving = false
		setv ov80 0
		part 1
*		self.Door.Animate.Open
		anim [0 1 2 3 4 5 6 7]
		over


		inst
*		foreach(Creature All All)
		epas 4 0 0
			doif targ <> null
*				targ.Unzombify()
				zomb 0
			endi
		next
*		ownr.DropPassengers(Creatures All All)
		dpas 0 0 0


		inst

*		if(From? != null)
		doif ov99 ne null
*			targ From
			targ ov99
*			if(From.Family = Creature)			
			doif fmly eq 4
*				From.Stim(TraveledInLift, 1)
				stim writ targ 94 1
			endi
			targ ownr
*			From? = null
			seta ov99 null
		endi


*		IsActive = 0
		setv ov00 0
		part 0
		anim [0]

*		foreach(ZootC3LiftButton)
		enum 2 12 24402
*			if(targ.Lift = self)
			doif ov16 = ownr
*				targ.DeactivateSelf()			
				mesg writ targ 1501
				targ ownr
				unlk
			endi
		next
		unlk
		snde "ding"
		part 2
		anim [4]
		part 3
		anim [5]
		bhvr 3
	endi
endm

*ZootC3Lift.FindButtonUpAndGo()
scrp 3 1 24402 2000
	inst
	doif from = pntr
		targ pntr
		anim [1 0]
		targ ownr
	endi

*	UpButton.Pose.6
	part 2
	anim [6]
*	DownButton.Pose.6
	part 3
	anim [6]
*	IsAnyoneActive = IsActive
	addv va66 ov00
*	foreach(ZootC3Lift)
	enum 2 12 24402
		doif ov16 = ownr
*			if(someoneIsActive || targ.IsActive)
*				someoneIsActive = true
			addv va66 ov00
		endi
	next
	targ ownr
	slow
*	if(!someoneIsActive)
	doif va66 = 0
*		selfId = unid
		setv va00 unid
*		destinationButton? = null
		seta va16 null
*		closestButton = 999999
		setv va88 999999
*		foreach(ZootLiftButton).Where(targ.Lift = self)
		enum 2 12 24402
			doif ov16 = ownr
*				distance = RelativeY(self, targ)
				setv va50 rely ownr targ
*				if(distance < 0)
				doif va50 < 0
*					absDistance = AbsoluteValue(distance)
					setv va51 va50
					absv va51
*					if(absDistance < closestButton && absDistance > 10)
					doif va51 < va88 and va51 > 10
*						closestButton = absDistance
						setv va88 va51
*						destinationButton? = targ
						seta va16 targ
					endi
				endi
			endi
		next
		doif va16 = null
		endi
*		if(destinationButton != null)
		doif va16 <> null
*			self.Base.Pose.3
			part 0
			anim [3]
*			destinationButton.Push()
			mesg writ va16 0
		endi

	else
		part 2
		anim [4]
		part 3
		anim [5]
	endi

endm

*ZootC3Lift.FindButtonDownAndGo()
scrp 3 1 24402 2001
	inst
	doif from = pntr
		targ pntr
		anim [1 1 1 0]
		targ ownr
	endi


	part 2
	anim [6]
	part 3
	anim [6]
	snde "clik"
	addv va66 ov00
	enum 2 12 24402
		doif ov16 = ownr
			addv va66 ov00
		endi
	next
	targ ownr
	slow
	doif va66 = 0

		setv va00 unid
		seta va16 null
		setv va88 999999
		enum 2 12 24402
			doif ov16 = ownr
				setv va50 rely ownr targ
				doif va50 > 0
					setv va51 va50
					absv va51
					doif va51 < va88 and va51 > 20
						setv va88 va51
						seta va16 targ
					endi
				endi
			endi
		next
		doif va16 = null
		endi
		doif va16 <> null
			part 0
			anim [2]
			mesg writ va16 0
		endi
	else
		part 2
		anim [4]
		part 3
		anim [5]
	endi

endm


*ZootC3Lift.Hit:Hit
scrp 2 12 24402 3
	sndc "hit_"
	prt: bang rand 60 100
	stim writ from 92 1
endm

rscr
enum 3 1 24402
	kill targ
next
scrx 3 1 24402 1
scrx 3 1 24402 2
scrx 3 1 24402 9
scrx 3 1 24402 1500
scrx 3 1 24402 2000
scrx 3 1 24402 2001


enum 2 12 24402
	kill targ
next
scrx 2 12 24402 1
scrx 2 12 24402 2
scrx 2 12 24402 1500
scrx 2 12 24402 1501
scrx 2 12 24402 1000